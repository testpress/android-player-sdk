plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

ext {
    useMedia3Library = true
}

android {
    namespace 'com.tpstream.player'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
        if (useMedia3Library) {
            manifestPlaceholders = [serviceClassName: "androidx.media3.exoplayer.scheduler.PlatformScheduler\$PlatformSchedulerService"]
        } else {
            manifestPlaceholders = [serviceClassName: "com.google.android.exoplayer2.scheduler.PlatformScheduler\$PlatformSchedulerService"]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    sourceSets{
        androidTest.assets.srcDirs += file("$projectDir/schemas".toString())
    }

    preBuild.dependsOn "runPlayerLibraryMappingTasks"
}

def media3playerVersion = "1.4.0-rc01"
def exoPlayer2Version = "2.19.1"
def room_version = "2.6.0"

dependencies {

    api 'androidx.core:core-ktx:1.13.1'
    api 'androidx.appcompat:appcompat:1.7.0'
    api 'com.google.android.material:material:1.12.0'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.3'
    api 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.3'
    api "com.google.code.gson:gson:2.11.0"
    api "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    api "androidx.room:room-ktx:$room_version"
    api 'androidx.fragment:fragment-ktx:1.8.1'
    api 'io.sentry:sentry-android:7.12.0'
    if (useMedia3Library) {
        api "androidx.media3:media3-exoplayer:$media3playerVersion"
        api "androidx.media3:media3-ui:$media3playerVersion"
        api "androidx.media3:media3-exoplayer-dash:$media3playerVersion"
        api "androidx.media3:media3-exoplayer-hls:$media3playerVersion"
        api "androidx.media3:media3-datasource-okhttp:$media3playerVersion"
    } else {
        api "com.google.android.exoplayer:exoplayer:$exoPlayer2Version"
        api "com.google.android.exoplayer:exoplayer-ui:$exoPlayer2Version"
        api "com.google.android.exoplayer:exoplayer-dash:$exoPlayer2Version"
        api "com.google.android.exoplayer:exoplayer-hls:$exoPlayer2Version"
        api "com.google.android.exoplayer:extension-okhttp:$exoPlayer2Version"
    }
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"
}

apply from: rootProject.file('gradle/gradle-mvn-build-packages.gradle')
apply from: rootProject.file('gradle/exoplayerMappingTasks.gradle')