plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

android {
    namespace 'com.tpstream.player'
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    sourceSets{
        androidTest.assets.srcDirs += file("$projectDir/schemas".toString())
    }

    preBuild.dependsOn "copyPlayerViewXML"
}

def media3playerVersion = "1.0.2"
def exoPlayer2Version = "2.19.1"
def room_version = "2.5.2"
def useMedia3Library = false

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation "com.google.code.gson:gson:2.10"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation 'androidx.fragment:fragment-ktx:1.5.4'
    implementation 'io.sentry:sentry-android:6.4.2'
    if (useMedia3Library) {
        implementation "androidx.media3:media3-exoplayer:$media3playerVersion"
        implementation "androidx.media3:media3-ui:$media3playerVersion"
        implementation "androidx.media3:media3-exoplayer-dash:$media3playerVersion"
        implementation "androidx.media3:media3-exoplayer-hls:$media3playerVersion"
        implementation "androidx.media3:media3-datasource-okhttp:$media3playerVersion"
    } else {
        implementation "com.google.android.exoplayer:exoplayer:$exoPlayer2Version"
        implementation "com.google.android.exoplayer:exoplayer-ui:$exoPlayer2Version"
        implementation "com.google.android.exoplayer:exoplayer-dash:$exoPlayer2Version"
        implementation "com.google.android.exoplayer:exoplayer-hls:$exoPlayer2Version"
        implementation "com.google.android.exoplayer:extension-okhttp:$exoPlayer2Version"
    }
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.2'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"
}

apply from: rootProject.file('gradle/gradle-mvn-build-packages.gradle')

tasks.register("libraryMapping") {
    dependsOn tasks.copyPlayerControlViewXML
}


tasks.register("deleteAllMappingFiles", Delete) {
    delete layout.projectDirectory.dir("src/main/java/com/tpstream/player/Mapping.kt")
    delete layout.projectDirectory.dir("src/main/res/layout/exo_player_control_view.xml")
    delete layout.projectDirectory.dir("src/main/res/layout/exo_styled_player_control_view.xml")
}


tasks.register("copyTypeAliseFile", Copy)  {
    dependsOn tasks.deleteAllMappingFiles
    if (useMedia3Library) {
        def oldPath = project.provider {
            layout.projectDirectory.file("src/main/assets/Media3PlayerMapping.kt")
        }
        def newPath = project.provider {
            layout.projectDirectory.dir("src/main/java/com/tpstream/player")
        }
        from oldPath
        into newPath
        rename 'Media3PlayerMapping.kt', 'Mapping.kt'
    } else {
        def oldPath = project.provider {
            layout.projectDirectory.file("src/main/assets/ExoPlayerMapping.kt")
        }
        def newPath = project.provider {
            layout.projectDirectory.dir("src/main/java/com/tpstream/player")
        }
        from oldPath
        into newPath
        rename 'ExoPlayerMapping.kt', 'Mapping.kt'
    }
}

tasks.register("copyPlayerControlViewXML", Copy)  {
    dependsOn tasks.copyTypeAliseFile
    if (useMedia3Library) {
        def oldPath = project.provider {
            layout.projectDirectory.file("src/main/assets/exo_player_control_view.xml")
        }
        def newPath = project.provider {
            layout.projectDirectory.dir("src/main/res/layout")
        }
        from oldPath
        into newPath
    } else {
        def oldPath = project.provider {
            layout.projectDirectory.file("src/main/assets/exo_styled_player_control_view.xml")
        }
        def newPath = project.provider {
            layout.projectDirectory.dir("src/main/res/layout")
        }
        from oldPath
        into newPath
    }
}

tasks.register("copyPlayerViewXML", Copy)  {
    dependsOn tasks.copyPlayerControlViewXML
    if (useMedia3Library) {
        def oldPath = project.provider {
            layout.projectDirectory.file("src/main/assets/exo_player_view.xml")
        }
        def newPath = project.provider {
            layout.projectDirectory.dir("src/main/res/layout")
        }
        from oldPath
        into newPath
        rename 'exo_player_view.xml', 'mapped_exo_player_view.xml'
    } else {
        def oldPath = project.provider {
            layout.projectDirectory.file("src/main/assets/exo_styled_player_view.xml")
        }
        def newPath = project.provider {
            layout.projectDirectory.dir("src/main/res/layout")
        }
        from oldPath
        into newPath
        rename 'exo_styled_player_view.xml', 'mapped_exo_player_view.xml'
    }
}